plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform()

    maxHeapSize = '512m'
    maxParallelForks(Runtime.getRuntime().availableProcessors())

    afterSuite { desc, result ->
        def timeElapsed = (result.endTime - result.startTime) / 1000
        logger.lifecycle("Tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${timeElapsed} sec")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.httpcomponents:httpclient:4.5.10'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.10.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.2'

    implementation 'org.slf4j:slf4j-api:1.7.28'

    compileOnly 'com.itextpdf:itext7-core:7.1.8'

    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'commons-io:commons-io:2.4'
    testImplementation 'com.itextpdf:itext7-core:7.1.8'

    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.slgl'
            artifactId = 'slgl-java'
            version = '1.0.0'

            from components.java

            pom {
                name = 'SLGL Java client library'
                description = 'Java client library for SLGL API'
                url = 'https://github.com/sigia-com/slgl-java'
                licenses {
                    license {
                        name = 'MIT license'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        name = 'Securion Payments Group AG'
                        email = 'support@securionpay.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:sigia-com/slgl-java.git'
                    developerConnection = 'scm:git:git@github.com:sigia-com/slgl-java.git'
                    url = 'https://github.com/sigia-com/slgl-java.git'
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }

            url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
